REBOL [
	Title:   "Make unit tests for utf-8-utils verify"
	Author:  "Peter W A Wood"
	File: 	 %make-verify-test.r
	Version: 0.0.1
	Rights:  "Copyright (C) 2011 Peter W A Wood. All rights reserved."
	License: "BSD-3 - https://github.com/dockimbel/Red/blob/master/BSD-3-License.txt"
]

file-out: %verify-test.reds
test-num: 0

;; find the location of quick-test.reds
qt-path: join head remove back tail read %../../path-to-red.txt [
  "red-system/tests/quick-test/quick-test.reds"
]

write-line: func [s [string!]] [
  write/append/binary file-out join s "^(0A)"
]

write-test: func [
  i [integer!] 
  s [string!]
  /local ns iss
][
  ns: copy {"}
  test-num: test-num + 1
  write-line join {  --test-- "verify-} [test-num {"}]
  foreach c s [
    append ns either all [
      c < 127
      c > 31
    ][
      c
    ][
      iss: to-hex to integer! c
      join "^^(" [iss/7 iss/8 ")"]
    ]
    ns
  ]

  write-line join {  --assert } compose [i " = PWAW-UU-verify "  ns {"}] 
]

two-char-passes: func [
  pre   [string!]
  post  [string!]
][
  foreach first-char ["^(C2)" "^(DF)"] [
    foreach second-char ["^(80)" "^(BF)" ] [
      write-test 0 join pre [first-char second-char post]
    ]
  ]  
]

two-char-fails: func [
  pos   [integer!]
  pre   [string!]
  post  [string!]
][
  foreach first-char ["^(C2)" "^(DF)"] [
    foreach second-char ["^(7F)" "^(C0)" ] [
      write-test pos join pre [first-char second-char post]
    ]
  ]  
]

three-char-passes: func [
  pre [string!]
  post [string!]
][
  foreach first-char ["^(E0)" "^(EF)"] [
    foreach second-char ["^(80)" "^(BF)" ] [
      foreach third-char ["^(80)" "^(BF)" ] [
        write-test 0 join pre [first-char second-char third-char post]
      ]
    ]
  ]
]

three-char-fails: func [
  pos [integer!]
  pre [string!]
  post [string!]
][
  foreach first-char ["^(E0)" "^(EF)"] [
    foreach second-char ["^(7F)" "^(C0)" ] [
      foreach third-char ["^(7F)" "^(C0)" ] [
        write-test pos join pre [first-char second-char third-char post]
      ]
    ]
  ]
]

four-char-passes: func [
  pre [string!]
  post [string!]
][
  foreach first-char ["^(F0)" "^(F4)"] [
    foreach second-char ["^(80)" "^(BF)" ] [
      foreach third-char ["^(80)" "^(BF)" ] [
        foreach fourth-char ["^(80)" "^(BF)" ] [
        write-test 0 join pre [first-char second-char third-char
                               fourth-char post]
        ]
      ]
    ]
  ]
]

four-char-fails: func [
  pos [integer!]
  pre [string!]
  post [string!]
][
  foreach first-char ["^(F0)" "^(F4)"] [
    foreach second-char ["^(7F)" "^(C0)" ] [
      foreach third-char ["^(7F)" "^(C0)" ] [
        foreach fourth-char ["^(7F)" "^(C0)" ] [
          write-test pos join pre [first-char second-char third-char
                                   fourth-char post]
        ]
      ]
    ]
  ]
]


print "Starting to make scan-utf-8 tests"
write/binary file-out ""
write-line {Red/System [}
write-line {	Title:   "utf-8-utils verify unit tests"}
write-line {	Author:  "Peter W A Wood"}
write-line {	File: 	 %verify-test.reds}
write-line {	Version: 0.0.1}
write-line {	Rights:  "Copyright (C) 2011 Peter W A Wood. All rights reserved."}
write-line {	License: "BSD-3 - https://github.com/dockimbel/Red/blob/master/BSD-3-License.txt"}
write-line {]}
write-line ""
write-line ";; This file is generated by make-verfiy-testr"
write-line ";; DO NOT EDIT IT MANUALLY"
write-line ""
write-line {#include %../utf-8-utils.reds}
write-line join {#include %} qt-path 
write-line ""

write-line {~~~start-file~~~ "verify"}

write-test 0 "a"
write-test 0 "a very straightforward string"
write-test 1 "^(C0)"
write-test 21 "12345678901234567890^(C0)"
write-test 1 "^(C1)"
write-test 1 "^(F5)"
two-char-passes "" ""
two-char-passes "a simple string" "another simple string"
two-char-fails 1 "" ""
two-char-fails 11 "0123456789" "0123456789"
three-char-passes "" ""
three-char-passes "string" "string"
three-char-fails 1 "" ""
three-char-fails 7 "string" "string"
four-char-passes "" ""
four-char-passes "string" "nonsense"
four-char-fails 1 "" ""
four-char-fails 5 "abcd" "efghijlk"

write-line {~~~end-file~~~}

print ["Completed" test-num "tests written"]




